Import('env')

env.Append(LIBS='freesteam')
env.Append(LIBPATH='..')

import platform
import glob
passfiles = glob.glob("*.cpass.cpp")
failfiles = glob.glob("*.cfail.cpp")

#-------------------
# Expected PASS buils

for i in passfiles:
	env.Program(i)

#-------------------
# Expected FAIL buils

def generate_xfail(source, target, env, for_signature):
	if platform.system()=="Windows":
		cmd = '$CXX $CXXFLAGS $CPPFLAGS $_CPPDEFFLAGS $_CPPINCFLAGS -o %s %s $_LIBDIRFLAGS $_LIBFLAGS' % (target[0], source[0])
		return "("+cmd+" || EXIT 0) && EXIT 1"
	else:
		cmd = '$CXX $CXXFLAGS $CPPFLAGS $_CPPDEFFLAGS $_CPPINCFLAGS -o %s %s $_LIBDIRFLAGS $_LIBFLAGS' % (target[0], source[0])		
		return "("+cmd+" || exit 0) && exit 1"
	

bld = Builder(generator = generate_xfail, suffix = '.exe', src_suffix = '.cpp')

env['BUILDERS']['XFail'] = bld


for i in failfiles:
	env.XFail(i)
