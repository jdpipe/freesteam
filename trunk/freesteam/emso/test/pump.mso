#* 
    freesteam stream integration for use in steam system calculations
    
    Based on steam.mso by Rafael de Pelegrini Soares,

    Copyright 2002-2005 Rafael de Pelegrini Soares.
    Modified by John Pye 2005 http://freesteam.sf.net
    
    This copyright statement must not be deleted and must be
    included in any modification or adaptation of this Model.
*#

using "steamequipment";

#*
    Steam pump

    Includes isentropic efficiency

    You can specify one of the pressures (In or Out) plus P or eta
*#
Model Pump as SteamEquipment_ph # 8 + 3 - 4 = 7
	
	PARAMETERS
	freesteam_es as CalcObject(File="emsofreesteam");
	
    VARIABLES
    eta as Efficiency(Brief="Pump efficiency");
    v   as SpecificVolume(Brief="Inlet specific volume", Default=0.001);
	h_es as SpecificEnthalpy(Brief = "Isentropic output enthalpy");
	
	CONNECTIONS
	#In.mdot to Out.mdot;
	
    EQUATIONS
    
    v = In.freesteam.v_ph(In.p, In.h);
    
    # "Energy balance"
    Wdot * eta = In.mdot * Dp * v;
    
    # FIXME: Volume at In or Out conditions ?
	
	"Isentropic output"
	h_es = freesteam_es.h_ps(Out.p, In.s);
    
    "Efficiency"
    (Out.h - In.h) = eta * (h_es - In.h);
	
	"Zero Volume"
	H = 0 * "J";
	m = 0 * "kg";
	
end

Model PumpWithCurve as Pump
	
	PARAMETERS

	VARIABLES
	# constants that define the pump curve:
	z_max as Length(Brief="Zero-Flow Head");
	Vdot_max as VolumeFlowRate(Brief="Zero-Head Flow Rate (vol)");
	N_1 as Frequency(Brief="Reference Speed", Unit="1/min");
	
	# variables that define the operating point:
	N as Frequency(Brief="Pump speed (RPM)", Unit="1/min", Default=100);
	z as Length(Brief="Pump head");
	Vdot as VolumeFlowRate(Brief="Pump flow rate (vol)");
	
	# internal variables
	z_1 as Length(Brief="Pump head");
	a as Real(Unit="m/(m^6/s^2)");
	Vdot_1 as VolumeFlowRate(Brief="Pump flow rate (vol)");
	
	EQUATIONS
	"a"
	a = z_max / Vdot_max^2;
	
	"pump curve parabola"
	z_1 = a * (Vdot_max^2 - Vdot_1^2);
	
	"Vdot"
	Vdot = Vdot_1 * N / N_1;
	
	"H"
	z = z_1 * (N/N_1)^2;
	# P = P_1 * (N/N_1)^3; # if we have a P_1(Q) curve, then we will be able to imply eta(Q,N) but at the moment, this is not implemented.
	
	"H to Dp"
	Dp = 9.81 * "N/kg" * z / v;

end
