#* 
    freesteam stream integration for use in steam system calculations
    
    Based on steam.mso by Rafael de Pelegrini Soares,

    Copyright 2002-2005 Rafael de Pelegrini Soares.
    Modified by John Pye 2005 http://freesteam.sf.net
    
    This copyright statement must not be deleted and must be
    included in any modification or adaptation of this Model.
*#

using "steamequipment";

#*
    Steam pump

    Includes isentropic efficiency

    You can specify one of the pressures (In or Out) plus P or eta
*#
Model Pump as SteamEquipment_ph # 8 + 3 - 4 = 7
	
	PARAMETERS
	freesteam_es as CalcObject(File="emsofreesteam");
	freesteam_v as CalcObject(File="emsofreesteam");
	
    VARIABLES
    P   as Power(Brief="Nominal power");
    eta as Efficiency(Brief="Pump efficiency");
    v   as SpecificVolume(Brief="Inlet specific volume", Default=0.001);
	h_es as SpecificEnthalpy(Brief = "Isentropic output enthalpy");
	Dp as PressureDifference;
    
	CONNECTIONS
	#In.mdot to Out.mdot;
	
    EQUATIONS
    "Mass balance"
    Out.mdot = In.mdot;
    
    v = freesteam_v.v_ph(In.p, In.h);
    
    # "Energy balance"
    P * eta = In.mdot * Dp * v;
    
    # FIXME: Volume at In or Out conditions ?
	
	"Isentropic output"
	h_es = freesteam_es.h_ps(Out.p, In.s);
    
    "Efficiency"
    (Out.h - In.h) = eta * (h_es - In.h);
	
    "Pressure difference"
    Dp = Out.p - In.p;        
end

Model PumpWithCurve as Pump
	
	PARAMETERS

	VARIABLES
	# constants that define the pump curve:
	H_max as Length(Brief="Zero-Flow Head");
	Q_max as VolumeFlowRate(Brief="Zero-Head Flow Rate (vol)");
	N_1 as Frequency(Brief="Reference Speed", Unit="1/min");
	
	# variables that define the operating point:
	N as Frequency(Brief="Pump speed (RPM)", Unit="1/min", Default=100);
	H as Length(Brief="Pump head");
	Q as VolumeFlowRate(Brief="Pump flow rate (vol)");
	
	# internal variables
	H_1 as Length(Brief="Pump head");
	a as Real(Unit="m/(m^6/s^2)");
	Q_1 as VolumeFlowRate(Brief="Pump flow rate (vol)");
	
	EQUATIONS
	"a"
	a = H_max / Q_max^2;
	
	"pump curve parabola"
	H_1 = a * (Q_1-Q_max) * (Q_1-Q_max);
	
	"Q"
	Q = Q_1 * N / N_1;
	
	"H"
	H = H_1 * (N/N_1)^2;
	# P = P_1 * (N/N_1)^3; # if we have a P_1(Q) curve, then we will be able to imply eta(Q,N) but at the moment, this is not implemented.
	
	"H to Dp"
	Dp = 9.81 * "N/kg" * H / v;

end
