#* 
    freesteam stream integration for use in steam system calculations
    
    Based on steam.mso by Rafael de Pelegrini Soares,

    Copyright 2002-2005 Rafael de Pelegrini Soares.
    Modified by John Pye 2005 http://freesteam.sf.net
    
    This copyright statement must not be deleted and must be
    included in any modification or adaptation of this Model.
*#

using "steamequipment";

#* 
    Ideal (isentropic) turbine
*#
Model Turbine_Ideal as SteamEquipment # DF = 10 + 1 - 3 = 8

    VARIABLES
    out Out as Stream_ph; # overrides SteamEquipment::Out
	P  as Power(Brief="Turbine Power");

    EQUATIONS
    "Mass Balance"
    In.mdot = Out.mdot;
    
    "Turbine power"
    P = In.mdot * (In.h - Out.h);
    
    "Isentropic constraint"
    Out.s = In.s;
end


#* 
    Turbine (non-isentropic, with efficiency specified)
*#
Model Turbine as SteamEquipment # DF = 10 + 1 - 3 = 8

    VARIABLES
    eta as Efficiency(Brief="Tubine efficiency");
    out Out as Stream_ph; # Overrides SteamEquipment::Out
    P as Power;
    
    EQUATIONS
    "Mass Balance"
    In.mdot = Out.mdot;
    
    "Potency"
    P = In.mdot * (In.h - Out.h);
    
    "Efficiency"
    (Out.h - In.h) = eta * (freesteam.h_ps(Out.p, In.s) - In.h);

end