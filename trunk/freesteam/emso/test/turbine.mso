#* 
    freesteam stream integration for use in steam system calculations
    
    Based on steam.mso by Rafael de Pelegrini Soares,

    Copyright 2002-2005 Rafael de Pelegrini Soares.
    Modified by John Pye 2005 http://freesteam.sf.net
    
    This copyright statement must not be deleted and must be
    included in any modification or adaptation of this Model.
*#

using "steamequipment";

#* 
    Ideal (isentropic) turbine
*#
Model Turbine_ideal as SteamEquipment_ps # 8 + 1 - 3 = 6 DF

    VARIABLES
    P  as Power(Brief="Turbine Power");
	x as Quality(Brief="Outlet quality");
    Dp as PressureDifference(Upper = 0);
	
	CONNECTIONS
	#In.mdot to Out.mdot;
	#In.s to Out.s;
	
    EQUATIONS # DF = -3
    "Mass Balance"
    In.mdot = Out.mdot;
    
    "Turbine power"
    P = In.mdot * In.h - Out.mdot * Out.h;

	"Pressure drop"
	Dp = Out.p - In.p;
	
    "Isentropic constraint"
    Out.s = In.s;
	
	"Outlet quality"
	x = Out.freesteam.x_ps(Out.p,Out.s);
end


#* 
    Turbine (non-isentropic, with efficiency specified)
*#
Model Turbine as SteamEquipment_ph # 8 + 4 - 5 = 7 DF

	PARAMETERS
	freesteam as CalcObject(File="emsofreesteam");
	
    VARIABLES # DF = 4
    eta as Efficiency(Brief="Turbine efficiency"); # DF = 1
	h_es as SpecificEnthalpy(Default = 3000, Lower=150, Upper = 4300);
    P as Power(Lower = 0); # DF = 1
    Dp as PressureDifference(Upper = 0);
	x as Quality(Brief="Outlet quality");
	
    CONNECTIONS
    # In.mdot to Out.mdot;
	
    EQUATIONS # DF = -5
    "Mass Balance"
    In.mdot = Out.mdot;
    
    #"Turbine power"
    P = In.mdot * In.h - Out.mdot * Out.h;
    
    "Isentropic output enthalpy"
    h_es = freesteam.h_ps(Out.p, In.s);
    
    "Efficiency"
    (Out.h - In.h) = eta * (h_es - In.h);
	
	"Pressure drop"
	Dp = Out.p - In.p;
	
	"Outlet quality"
	x = Out.freesteam.x_ph(Out.p,Out.h);	
end

#* 
    Turbine (non-isentropic, with efficiency specified)
*#
Model Turbine_ps as SteamEquipment_ps # 8 + 4 - 5 = 7 DF

	PARAMETERS
	freesteam as CalcObject(File="emsofreesteam");
    
    VARIABLES # DF = 4
    eta as Efficiency(Brief="Turbine efficiency");
	h_es as SpecificEnthalpy(Default = 3000, Lower=150, Upper = 4300);
    P as Power(Brief="Turbine Power (thermal)", Lower = 0);
    Dp as PressureDifference(Upper = 0);
	x as Quality(Brief="Outlet quality");
    
    EQUATIONS #DF = -5
    "Mass Balance"
    In.mdot = Out.mdot;
    
    #"Turbine power"
    P = In.mdot * In.h - Out.mdot * Out.h;
    
    "Isentropic output enthalpy"
    h_es = freesteam.h_ps(Out.p, In.s);
    
    "Isentropic Efficiency"
    (Out.h - In.h) = eta * (h_es - In.h);

	"Pressure drop"
	Dp = In.p - Out.p;

	"Outlet quality"
	x = Out.freesteam.x_ps(Out.p,Out.s);	
end
