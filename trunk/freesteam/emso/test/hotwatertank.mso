#*
	Model of simple hot water tank
	
	waterflows in and out and the temperature of the water in the tank changes
    assuming constant mixing, no stratification etc.

	Tank is assumed open at the top so that the outlet pressure is determined 
	only by the level of water in the tank
*#

using "steamequipment";

Model HotWaterTank as SteamEquipment_uv
	
	VARIABLES
	y as Length(Brief="Height of water in tank", Unit="m", Lower=0, Upper=10);
	rho as Density(Brief="Density of water in tank", Lower=600, Upper=1300, Unit="kg/m^3");
	A as Area(Brief="Area of tank crosssection (horizontal plane)");
	h as SpecificEnthalpy(Brief="Average specific enthalpy in tank");
	p_amb as Pressure(Brief="Ambient pressure (tank is open at top)", Lower=0);
	T as Temperature(Brief="Temperature of water in tank", Lower=273.15);
	
	EQUATIONS
	# mass is density by volume
	m = rho * A * y;
	
	# total enthalpy
	H = m * h;
	
	# Even mixing: what comes out is the average enthalpy and temperature
	Out.h = h;
	Out.T = T;
	
	# Pressure at base is ambient plus static head
	Out.p = p_amb + rho * 9.8 * "N/kg" * y;
	
	# No heating
	Qdot = 0 * "W";
	
	# Trivial work: forcing water up against atmospheric pressure as control volume changes size
	Wdot =  0 * "W"; # p_amb * diff(m) / rho;
	
	# density can be calculated using the outlet state, because of the mixing
	# (assume that variation with pressure is negl)
	rho = 1 / Out.v;
	
end
