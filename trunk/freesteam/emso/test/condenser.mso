#* 
    freesteam stream integration for use in steam system calculations
    
    Based on steam.mso by Rafael de Pelegrini Soares,
    Copyright 2002-2005 Rafael de Pelegrini Soares.

	Modified by John Pye 2005 http://freesteam.sf.net
*#

using "steamequipment";

#*  Steam Condenser.

    This model receives one In strem from which a quantity of heat q
    is removed.

    Specification for this model can be q or dt. The specification of q
    should be prefered, once a bad specificafion of dt can lead singular
    systems (infinite solutions if the Out stream became saturated).
*#
Model Condenser as SteamEquipment_ph # 8 + 2 - 3 = 7
    
    VARIABLES
    q   as HeatRate(Brief="Heat removed from the stream");
    DT  as Temperature(Brief="Temperature difference");
    Dp  as PressureDifference(Brief="Pressure difference", Upper=0);
	UA  as HeatRatePerTemperature(Brief="Condenser UA value");
	x   as Quality;
	
    EQUATIONS
    "Mass balance"
    Out.mdot = In.mdot;
    
    "Energy balance (q extracted > 0)"
    q = - (Out.mdot * Out.h - In.mdot * In.h);
    
    "Heat transfer coefficient"
    q = -UA * DT;
    
    "Temperature difference"
    DT = Out.T - In.T;
    
    "Pressure difference"
    Dp = Out.p - In.p;
	
	"Outlet quality"
	x = Out.freesteam.x_ph(Out.p, Out.h);
	
#   GUESS # FIXME: implement GUESS on models!!
#   Out.h = 300 * "kJ/kg";

end

Model Condenser_pT as SteamEquipment_pT # 8 + 4 - 5 = 7
    
    VARIABLES
    q   as HeatRate(Brief="Heat removed from the stream");
    DT  as Temperature(Brief="Temperature difference");
    Dp  as PressureDifference(Brief="Pressure difference");
	UA  as HeatRatePerTemperature(Brief="Condenser UA value");
	
    EQUATIONS
    "Mass balance"
    Out.mdot = In.mdot;
    
    "Energy balance (q extracted > 0)"
    q = - (Out.mdot * Out.h - In.mdot * In.h);
    
    "Heat transfer coefficient"
    q = -UA * DT;
    
    "Temperature difference"
    DT = Out.T - In.T;
    
    "Pressure difference"
    Dp = Out.p - In.p;
    
end
