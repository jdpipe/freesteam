#* 
    freesteam stream integration for use in steam system calculations
    
    Based on steam.mso by Rafael de Pelegrini Soares,
    Copyright 2002-2005 Rafael de Pelegrini Soares.

	Modified by John Pye 2005 http://freesteam.sf.net
*#

using "steamequipment";

#*  Steam Condenser.

    This model receives one In strem from which a quantity of heat q
    is removed.

    Specification for this model can be q or dt. The specification of q
    should be prefered, once a bad specificafion of dt can lead singular
    systems (infinite solutions if the Out stream became saturated).
*#
Model Condenser as SteamEquipment_ph # 8 + 2 - 3 = 7
    
    VARIABLES
    UA  as HeatRatePerTemperature(Brief="Condenser UA value");
	x   as Quality;

	EQUATIONS
	# Zero volume
	m = 0 * "kg";
	H = 0 * "J";
	
	# No work
	Wdot = 0 * "W";
	
	"Heat transfer coefficient"
    Qdot = UA * DT;
    	
	"Outlet quality"
	x = Out.freesteam.x_ph(Out.p, Out.h);
	
#   GUESS # FIXME: implement GUESS on models!!
#   Out.h = 300 * "kJ/kg";

end

Model Condenser_pT as SteamEquipment_pT # 8 + 4 - 5 = 7
    
    VARIABLES
    UA  as HeatRatePerTemperature(Brief="Condenser UA value");
	
	EQUATIONS
	# Zero volume
	m = 0 * "kg";
	H = 0 * "J";
	
	# No work
	Wdot = 0 * "W";
	
	"Heat transfer coefficient"
    Qdot = UA * DT;
	
	# Can't evaluate x at outlet in this case, since steam state
	# specified i.t.o. (p,T)
    
end

Model CondenserWithLoss as Condenser

	VARIABLES
    K as Positive(Brief="Minor loss coefficient for boiler");
	A_1 as Area(Brief="Boiler inlet pipe area");
		
    EQUATIONS
	"Pressure drop"
	Dp = - K * In.mdot^2 / In.freesteam.rho_ph(In.p,In.h) / A_1^2;
    
end
	