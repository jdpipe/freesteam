using "newtypes";
using "streams";

Model PipeNode
	
	PARAMETERS
	freesteam as CalcObject(File="emsofreesteam");
	A as Area;
	
	VARIABLES
	mdot as MassFlowRate;
	p as Pressure;
	u as SpecificInternalEnergy;
	rho as Density;
	mdoth as Real(Unit="kW");
	vel as Velocity;
	Qdotdash as Real(Unit="kW/m");
	tau as Stress;
		
	EQUATIONS
	mdoth = mdot * (u + p / rho);
	p = freesteam.p_uv(u,1/rho);
	vel = mdot / rho / A;

	Qdotdash = 0 * "kW/m";
	tau = 0 * "Pa";
end


Model Pipe2
	
	PARAMETERS
	n as Integer(Lower=3);
	L as Length;
	D as Length;
	eps as Length;
	dz as Length;
	A as Area;
	
	VARIABLES
	in In as Stream;
	out Out as Stream_ph;
	X(n) as PipeNode;
	
	EQUATIONS
	
	# Use central difference form for the interior nodes
		
	for i in [2:n-2]
		
		diff(X(i).rho) = - 1 / A * ( 0.5 * (X(i+1).mdot - X(i-1).mdot) / dz );

		diff(X(i).rho * X(i).u)
			= 1/A * ( X(i).Qdotdash - 0.5 * (X(i+1).mdoth - X(i-1).mdoth) / dz );
	
		1/A * diff(X(i).mdot) 
			= 0.5 * (X(i+1).p - X(i-1).p)/dz
				- 4 * X(i).p / D 
				- 0.5 * (X(i+1).rho * X(i+1).vel^2 - X(i-1).rho * X(i-1).vel^2) / dz;

	end

	# Use backwards difference equation for the last node
	
	diff(X(n).rho) = - 1 / A * ( (X(n).mdot - X(n-1).mdot) / dz );

	diff(X(n).rho * X(n).u)
		= 1/A * ( X(n).Qdotdash - (X(n).mdoth - X(n-1).mdoth) / dz );
	
	1/A * diff(X(n).mdot) 
		= (X(n).p - X(n-1).p)/dz
			- 4 * X(n).p / D 
			- (X(n).rho * X(n).vel^2 - X(n-1).rho * X(n-1).vel^2) / dz;
	
	# There are no equations for the first node; these are provided by the upstream equipment item
	# Note that central difference can be optimised to use a staggered grid, not done here.
	
	# TODO: does this formulation work correctly with reverse flow??
	
	X(1).p = In.p;
	X(1).u = In.freesteam.u_ph(In.p, In.h);
	X(1).mdot = In.mdot;
	
	Out.p = X(n).p;
	Out.h = X(n).u + X(n).p / X(n).rho;
	X(n).mdot = Out.mdot;
	
	SET
	dz = L/n;
	A = 3.14159265358 * D^2 / 4;
	X.A([1:n]) = A;

end