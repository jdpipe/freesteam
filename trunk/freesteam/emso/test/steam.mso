#* 
	freesteam stream integration for use in steam system calculations
	
	Based on steam.mso by Rafael de Pelegrini Soares,

	Copyright 2002-2005 Rafael de Pelegrini Soares.
	Modified by John Pye 2005 http://freesteam.sf.net
	
	This copyright statement must not be deleted and must be
	included in any modification or adaptation of this Model.
*#

using "newtypes";

#*
	Properties of steam

	The properties are defined here, but they are all remaining to be related,
	according to the requirements of whatever model is derived from this one.
*#
Model Steam_Base # 6 DF
	
	VARIABLES

	T as AbsoluteTemperature(    Lower=273.15, Upper=1073.15,Default = 600);
	p as Pressure(               Lower=0,      Upper=100,    Default = 10,   Unit = "MPa");
	h as SpecificEnthalpy(       Lower=0,      Upper=1e4,    Default=4000 );
	s as SpecificEntropy(        Lower=0,      Upper=10,     Default=7 );
	v as SpecificVolume(         Lower=0,      Upper=1100,   Default=0.1 );
	x as Quality;

end

Model Steam as Steam_Base # 6 + 1 -1 = 6 DF

	VARIABLES	
	u as SpecificInternalEnergy( Lower=0,      Upper=3800,   Default = 2200);

	EQUATIONS
	u = h + p * v;
	
end

Model Steam_ps as Steam # 6 - 4 = 2 DF

	PARAMETERS
	ext freesteam as CalcObject(Brief="Freesteam IAPWS calculator");
		
	EQUATIONS
	"Steam properties"
	[T, h, v, x] = freesteam.Thvx_ps(p, s);	

end


Model Steam_ph as Steam # 6 - 4 = 2 DF

	PARAMETERS
	ext freesteam as CalcObject(Brief="Freesteam IAPWS calculator");
		
	EQUATIONS
	"Steam properties"
	[T, s, v, x] = freesteam.Tsvx_ph(p, h);	

end

Model Steam_pu as Steam # 6 - 4 = 2 DF

	PARAMETERS
	ext freesteam as CalcObject(Brief="Freesteam IAPWS calculator");
		
	EQUATIONS
	"Steam properties"
	[T, s, v, x] = freesteam.Tsvx_pu(p,u);
	
end

Model Steam_waterT as Steam # 6 - 5 = 1 DF

	PARAMETERS
	ext freesteam as CalcObject(Brief="Freesteam IAPWS calculator");
		
	EQUATIONS
	"Steam properties"
	[p, s, v, h, x] = freesteam.psvhx_waterT(T);
	
end

Model Steam_steamT as Steam # 6 - 5 = 1 DF

	PARAMETERS
	ext freesteam as CalcObject(Brief="Freesteam IAPWS calculator");
		
	EQUATIONS
	"Steam properties"
	[p, s, v, h, x] = freesteam.psvhx_steamT(T);
	
end
