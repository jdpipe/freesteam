using "steamequipment";
using "pump";

#*
	SimpleLoop re-done with standard
	components
*#
Model SpringTank as SteamEquipment_ph
	
	PARAMETERS
	y_0 as Length;
	A as Area;
	k as Real(Unit="N/m", Lower=0, Upper=1e9, Default=1);
	
	VARIABLES
	v_e as SpecificVolume;
	y as Length;
	
	EQUATIONS
	v_e = Out.freesteam.v_ph(Out.p,Out.h);
	
	v_e * m = A * y;
	In.p = k * (y - y_0) / A;
	In.p = Out.p;
	
	Out.h = H / m;
	
	Wdot = In.p * A * diff(y);
	
	Qdot - Wdot = Out.h * Out.mdot - In.h * In.mdot + diff(H);
	In.mdot - Out.mdot = diff(m);
	Qdot = 0 * "W";

end

Model Valve as SteamEquipment_ph
	
	PARAMETERS
	K as Real;
	A as Area;
	
	VARIABLES
	v_i as SpecificVolume;
	vel as Velocity;
	
	EQUATIONS
	v_i = In.freesteam.v_ph(In.p,In.h);
	In.mdot * v_i / A = vel;
	Dp = - K * 0.5 / v_i * vel^2;
	
	Qdot = 0 * "W";
	Wdot = 0 * "W";
	H = 0 * "J";
	m = 0 * "kg";
	Out.mdot = In.mdot;
	Out.h = In.h;
	
end


FlowSheet SimpleLoopTest

	DEVICES
	PU as PumpSimple;
	TA1 as SpringTank;
	TA2 as SpringTank;
	VA as Valve;
	
	CONNECTIONS
	PU.Out to TA1.In;
	TA1.Out to VA.In;
	VA.Out to TA2.In;
	TA2.Out to PU.In;
	
	SET
	# Pump
	PU.eta = 0.65;
	
	# Tanks
	TA1.A = 1 * "m^2";
	TA1.k = 1 * "bar" / (0.1 * "m") * 1 * "m^2";
	TA1.y_0 = 0.8 * "m";
	TA2.A = 1 * "m^2";
	TA2.k = 1 * "bar" / (0.1 * "m") * 1 * "m^2";
	TA2.y_0 = 0.8 * "m";
	
	# Valve
	VA.A = 3.1415926 * 0.01^2 / 4 * "m^2";
	VA.K = 5;
	
	SPECIFY
	PU.Dp = 0.1 * "bar";

	INITIAL
	TA1.y = 1 * "m";
	TA1.H = 500 * "MJ";
	TA2.y = 1 * "m";
	TA2.H = 500 * "MJ";
	
	
	OPTIONS
	# differentiation="numeric";
	mode="steady";
	outputLevel="all";
end
