#*
	Model of buffer tank
	
	This is a fixed volume of water which will act to smooth out fluctuations 
	in upstream temperature, because of its thermal bulk.

	As opposed to the HotWaterTank model, the volume of fluid in this model
	is held constant.

	Assume perfect mixing all the time.
*#

using "steamequipment";

#*
	fixed-volume tank
	outlet assumes constant, perfect mixing of input stream with the mass holdup.

	Note that this model does NOT extend the SteamEquipment model!

	THIS MODEL WORKS
*#
Model BufferTank
	
	PARAMETERS
	V as Volume(Brief="Volume of buffer tank (set it to a constant)");
	
	VARIABLES
	in In  as Stream;
	out Out as Stream;
	U as Energy(Upper=1e10);
	m as Mass;
	Qdot as Power;
	Wdot as Power;
	u as SpecificInternalEnergy;
	v as SpecificVolume;
	vin as SpecificVolume;
	
	EQUATIONS
	# Mass conservation	
	In.mdot - Out.mdot = diff(m);
	v = V / m;
	
	# Energy conservation, no work, no volume
	In.h * In.mdot - Out.h * Out.mdot = diff(U);
	u = U / m;
	
	Qdot = 0 * "W";
	Wdot =  0 * "W";
	
	# no pressure drop in the tank
	Out.p = In.p;
	
	# Enthalpy by its definition
	Out.h = u + Out.p * v;
	
	# Volume conservation
	Out.mdot * v = In.mdot * vin;

	vin = In.freesteam.v_ph(In.p,In.h);	
	
	Out.s = Out.freesteam.s_ph(Out.p,Out.h);
	Out.T = Out.freesteam.T_ph(Out.p,Out.h);

end