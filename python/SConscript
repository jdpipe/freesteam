#!/usr/bin/env python
Import('env')
import re,subprocess,sys

envp = env.Clone()

# get settings for linking to Python

envp.ParseConfig('pkg-config $PYTHON_PKG --cflags --libs')

# check SWIG version

def get_swig_version(env):
	#print("SWIG='%s'" % env['SWIG'])
	cmd = [env['SWIG'],'-version']
	if sys.version_info[0]==2:
		output = subprocess.check_output(cmd)
	else:
		output = subprocess.check_output(cmd,encoding='utf-8')
	
	restr = "SWIG\\s+Version\\s+(?P<maj>[0-9]+)\\.(?P<min>[0-9]+)\\.(?P<pat>[0-9]+)\\s*$"
	expr = re.compile(restr,re.M);
	m = expr.search(output);
	if not m:
		return None
	maj = int(m.group('maj'))
	min = int(m.group('min'))
	pat = int(m.group('pat'))

	return (maj,min,pat)

def check_swig_version(context):
	try:
		context.Message("Checking version of SWIG... ")
		maj,min,pat = get_swig_version(context.env)
	except Exception as e:
		context.Result("Failed to detect version, or failed to run SWIG (%s)"% str(e))
		return False;
	
	context.env['SWIGVERSION']=tuple([maj,min,pat])
	
	if maj >= 3:
		context.Result("ok, %d.%d.%d" % (maj,min,pat))
		return True;
	else:
		context.Result("too old, %d.%d.%d" % (maj,min,pat))
		return False;
		
conf = envp.Configure(custom_tests={'CheckSwig':check_swig_version})

build_python=True
if not conf.CheckCHeader('Python.h'):
	build_python=False
if not conf.CheckSwig():
	build_python=False
else:
	conf.env.Append(SWIGFLAGS=['-python'])

envp = conf.Finish()

if build_python:

	envp.Append(
		LIBS=['freesteam']
		,CPPPATH=['#']
		,LIBPATH=['#']
	)

	import distutils.sysconfig, platform, os, sys

	# remove the flags that cause warnings from SWIG...
	# TODO can we do anything to avoid these warnings, otherwise?

	_cflags =  envp.get('CCFLAGS')
	for f in ['-Wconversion','-Wall']:
		if f in _cflags:
			_cflags.remove(f)
	envp.Replace(CCFLAGS=_cflags)

	if platform.system()=="Windows":
		envp['SHLIBSUFFIX']=".pyd"

	swiglib = envp.SharedLibrary("freesteam",['freesteam.i']
		, SHLIBPREFIX = '_'
	)
	env.Depends("freesteam.i","../config.h")

	env.Command("${INSTALL_ROOT}$INSTALL_PYTHON/freesteam",swiglib,Mkdir("${INSTALL_ROOT}$INSTALL_PYTHON/freesteam"))
	env.Install("${INSTALL_ROOT}$INSTALL_PYTHON/freesteam",[swiglib, 'freesteam.py','__init__.py'])

#	env['installedfiles'] += [env.InstallLibrary("${INSTALL_ROOT}$INSTALL_PYTHON",[swiglib])]
#	env['installedfiles'] += [env.Install("${INSTALL_ROOT}$INSTALL_PYTHON",['freesteam.py'])]

# vim:set ts=4 sw=4 noexpandtab ft=python:
