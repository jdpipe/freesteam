#!/usr/bin/env python
Import('env')

if env.get('HAVE_PYTHON'):

	python_env = env.Clone()

	python_env.Append(
		CPPPATH=['#']
		,LIBPATH=['#']
	)

	import distutils.sysconfig, platform, os, sys

	#------------------------------------------------------
	# SWIG scanner

	if platform.system()=="Windows":
		python_lib = "python%s%s"%(sys.version_info[0],sys.version_info[1])
	else:
		python_lib = "python%s.%s"%(sys.version_info[0],sys.version_info[1])

	python_env.Append(
		CPPPATH = [distutils.sysconfig.get_python_inc()]
		,LIBPATH = [distutils.sysconfig.PREFIX+"/libs"]
		, LIBS = ['freesteam',python_lib]
	)

	swigobj = python_env.SharedObject('freesteam.i'
		, SWIGFLAGS=['-python']
	)
	env.Depends("freesteam.i","../config.h")

	if platform.system()=="Windows":
		python_env['SHLIBSUFFIX']=".pyd"
	elif platform.system()=="Darwin":
		python_env['SHLIBSUFFIX']='.so'

	swiglib = python_env.SharedLibrary("freesteam",swigobj
		, SHLIBPREFIX = '_'
	)

	env['installedfiles'] += [env.InstallLibrary("${INSTALL_ROOT}$INSTALL_PYTHON",[swiglib])]
	env['installedfiles'] += [env.Install("${INSTALL_ROOT}$INSTALL_PYTHON",['freesteam.py'])]

