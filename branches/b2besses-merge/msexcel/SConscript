#!/usr/bin/env python
Import('env')
import os, sys, platform

if platform.system()=="Windows":

	env.SConscript(['xlw/SConscript'],'env')

	#print "LIBPATH =",env.get('LIBPATH')
	#print "GSLSTATIC = ",env.get('GSL_STATIC')
	xlenv = env.Clone()

	# FIXME we still need to figure out how to build 64-bit XLL, probably
	# not possible with the XLW 4.0 code we've included in the xlw/ dir.
	XLL64 = False

	libxlw_path = "xlw/lib"
	libxlw_name = 'xlw'
	if XLL64:
		libxlw_path = "$XLW/lib64"

	xlenv.Append(
		CPPPATH=["xlw/include","#"]
		,LIBPATH=["#/lib",libxlw_path]
		,LINKFLAGS=['-static','-s','-static-libgcc','-Wl,-Bstatic']
		,LIBS=['freesteam','user32']
	)

	subst_dict = {
		'@VERSION@':"$FREESTEAM_VERSION"
	}

	xlenv.Append(SUBST_DICT=subst_dict)
	iface_h = xlenv.SubstInFile('cppinterface.h.in')

	# Generate xlwcppinterface.cpp from cppinterface.h using xlw/xlw_ifgen
	iface = xlenv.XlwWrapper(iface_h)

	xlenv1 = xlenv.Clone()

	srcs = xlenv1.Glob("fstm_*.cpp") + iface

	xlenv1.Append(
		CPPPATH=env.get('GSL_CPPPATH')
	)

	if xlenv1['GSL_STATIC']:
		srcs = env.get('GSL_STATICLIBS',[]) + srcs
	else:
		xlenv1.Append(
			LIBS = env.get('GSL_LIBS')
			,LIBPATH = env.get('GSL_LIBPATH')
		)

	xlenv1.AppendUnique(
		LIBPATH=[".."]
		,LIBS=['xlw']
		,LINKFLAGS=['-Wl,msexcel/xlw/include/xlw/xlw.def']
	)

	# this is our Excel add-in, freesteam.xll:
	xlenv1.SharedLibrary("freesteam",srcs
		,SHLIBSUFFIX='.xll'
		,SHLIBPREFIX=''
	)

